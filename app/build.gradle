buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        flatDir {
            dirs 'libs'
        }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}
import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
// TODO: Uncomment following line to use Fabric
//apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    def String majorKey = 'MAJOR_VERSION'
    def String minorKey = 'MINOR_VERSION'
    def String versionCodeKey = 'DEBUG_VERSION_CODE'
    def versionPropertiesFile = file('version.properties')
    def Properties versionProperties = readVersionProperties(versionPropertiesFile)

    def df = new SimpleDateFormat("yyyy-MM-dd HH:mmZ")
    def String date = df.format(new Date(System.currentTimeMillis()))

    def getDate = { ->
        def dateFormatted = new Date()
        def formattedDate = dateFormatted.format('yyyyMMdd')
        return formattedDate
    }

    def currentCodeVersion = versionProperties[versionCodeKey].toString().toInteger()

    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    dexOptions {
        jumboMode true
    }
    defaultConfig {
        applicationId 'com.zype.android.template'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode currentCodeVersion
        versionName '1.5.0'
        buildConfigField "String", "VERSION", "\"$versionName\""
        manifestPlaceholders = [manifestApplicationId          : "${applicationId}",
                                // TODO: Provide valid app_id and google_project_number for OneSignal
                                onesignal_app_id               : "",
                                onesignal_google_project_number: ""]
    }

    productFlavors {
    }

    buildTypes {
        release {
            setVersionNameSuffix(".${currentCodeVersion}")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def int newVersionCode = currentCodeVersion + 1
            versionProperties[versionCodeKey] = newVersionCode.toString()
            store(versionPropertiesFile, versionProperties)
        }
        debug {
            setVersionNameSuffix(".${currentCodeVersion} DEBUG [${date}]")
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputNameNew = "Zype-Android" + "-" + variant.productFlavors.name + "-" + variant.buildType.name +
                    "-" + defaultConfig.versionName + "-" + getDate() + ".apk"
            output.outputFile = new File(output.outputFile.parent, outputNameNew)
        }
    }
}

public def readVersionProperties(File from) {
    if (from.canRead()) {
        def Properties result = new Properties()
        result.load(new FileInputStream(from))
        return result
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

public def store(File to, Properties properties) {
    properties.store(to.newWriter(), null)
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.4.1'
    compile project(':CastCompanionLibrary')
//    compile 'com.google.android.libraries.cast.companionlibrary:ccl:2.9.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:mediarouter-v7:25.3.1'
    compile 'com.android.support:customtabs:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.ns-developer:tagcloudview:0.1.0'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.12'
    compile 'com.onesignal:OneSignal:2.+@aar'

    compile ('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true;
    }

    compile 'com.google.android.gms:play-services-gcm:10.2.1'
    compile 'com.google.android.gms:play-services-analytics:10.2.1'
    compile 'com.google.android.gms:play-services-location:10.2.1'

    // IMA SDK
    compile 'com.google.ads.interactivemedia.v3:interactivemedia:3.5.2'
    compile 'com.google.android.gms:play-services-ads:10.2.1'

    // In-app billing
    compile 'com.android.billingclient:billing:dp-1'
}

apply plugin: 'com.google.gms.google-services'